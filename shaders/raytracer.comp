#version 430

struct Ray {
    vec3 origin;
    vec3 direction;
};

struct HitPayload {
    float hitDistance;
    vec3 position;
    vec3 normal;
    int materialIndex;
    bool hit;
};

struct Material {
    vec3 albedo;
    float roughness;
    float metallic;
    vec3 emissionColor;
    float emissionPower;
};

layout(local_size_x = 8, local_size_y = 8) in;

layout(rgba32f, binding = 0) uniform image2D colorbuffer;
layout(std430, binding = 1) readonly buffer sceneData{
    int[] voxelGrid;
};
uniform ivec3 gridSize;

uniform vec3 cameraPosition;
uniform mat4 inverseProjectionMatrix;
uniform mat4 inverseViewMatrix;
uniform int rendererRandom;
uniform int maxAccumulations;

const int bounces = 10;
const vec3 backgroundColor = vec3(0.6, 0.7, 0.9);
const float M_PI = 3.14159265358979323846;
const vec3 sun_dir = normalize(vec3(1,-1,1));
const vec3 sun_light_color = vec3(1);
const float sun_light_intensity = 0.5;
const vec3 player_light_color = vec3(1);
const float player_light_intensity = 100;


const Material[] materials = {
    Material(vec3(0,0,0), 0, 0, vec3(1), 0),
    Material(vec3(0.7,0.4,0), 1, 0, vec3(1,0,0), 0),
    Material(vec3(0,0.7,0.1), 1, 0, vec3(0,1,0), 0),
    Material(vec3(0,0.2,0.7), 1, 0, vec3(0,0,1), 0),
};


// Utility function to calculate intersection with grid boundaries
bool intersectGridBounds(Ray ray, out ivec3 voxelPosition, out float tEntry, out vec3 normal) {

    vec3 step = sign(ray.direction);

    vec3 tMin = (vec3(0.0) - ray.origin) / ray.direction;
    vec3 tMax = (vec3(gridSize) - ray.origin) / ray.direction;

    vec3 t1 = min(tMin, tMax);
    vec3 t2 = max(tMin, tMax);

    tEntry = max(max(t1.x, t1.y), t1.z);

    if(tEntry == t1.x){
        normal = vec3(-step.x, 0, 0);
    } else if(tEntry == t1.y){
        normal = vec3(0, -step.y, 0);
    } else{
        normal = vec3(0, 0, -step.z);
    }

    float tExit = min(min(t2.x, t2.y), t2.z);

    if (tEntry > tExit) {
        return false; // No intersection with grid
    }

    if (tEntry < 0){
        return false;
    }

    vec3 intersectionPoint = ray.origin + ray.direction * tEntry;

    const float epsilon = 1e-4;
    intersectionPoint += epsilon * normalize(ray.direction);

    voxelPosition = ivec3(floor(intersectionPoint));

    return true;
}

HitPayload traceRay(Ray ray) {
    HitPayload hitPayload;
    hitPayload.hitDistance = 0.0;
    hitPayload.normal = vec3(0.0);
    hitPayload.materialIndex = 0;
    hitPayload.hit = false;

    ivec3 voxelPosition;
    float t = 0;
    vec3 normal = vec3(0);

    vec3 invDir = 1.0 / ray.direction;
    ivec3 stepInGrid = ivec3(sign(ray.direction));

    // If the ray starts inside the grid, use the original ray origin
    if (!(ray.origin.x < 0.0 || ray.origin.x >= float(gridSize.x) ||
    ray.origin.y < 0.0 || ray.origin.y >= float(gridSize.y) ||
    ray.origin.z < 0.0 || ray.origin.z >= float(gridSize.z))) {
        voxelPosition = ivec3(floor(ray.origin));
    }

    // If it is outside check if it will intersect the grid or not
    else{
        if (!intersectGridBounds(ray, voxelPosition, t, normal)){
            return hitPayload;
        }

    }

    // Calculate tMax and tDelta
    vec3 tMax = (vec3(voxelPosition + stepInGrid*0.5 + 0.5) - ray.origin) * invDir;
    vec3 tDelta = abs(invDir);

    int i = 0;

    while (i<10000) {
        i++;

        // Check if the current voxel is out of the grid bounds
        if (voxelPosition.x < 0 || voxelPosition.x >= gridSize.x ||
        voxelPosition.y < 0 || voxelPosition.y >= gridSize.y ||
        voxelPosition.z < 0 || voxelPosition.z >= gridSize.z) {
            return hitPayload;
        }

        // Check if the current voxel is a block
        if (voxelGrid[voxelPosition.x + voxelPosition.z * gridSize.x + voxelPosition.y * gridSize.x * gridSize.z] != 0) {
            hitPayload.materialIndex = voxelGrid[voxelPosition.x + voxelPosition.z * gridSize.x + voxelPosition.y * gridSize.x * gridSize.z];
            hitPayload.hitDistance = t;
            hitPayload.normal = normal;
            hitPayload.position = ray.direction * t + ray.origin;
            hitPayload.hit = true;
            return hitPayload; // Hit
        }

        // Traverse the grid
        if (tMax.x < tMax.y) {
            if (tMax.x < tMax.z) {
                voxelPosition.x += stepInGrid.x;
                t = tMax.x;
                normal = vec3(-stepInGrid.x, 0, 0);
                tMax.x += tDelta.x;
            } else {
                voxelPosition.z += stepInGrid.z;
                t = tMax.z;
                normal = vec3(0, 0, -stepInGrid.z);
                tMax.z += tDelta.z;
            }
        } else {
            if (tMax.y < tMax.z) {
                voxelPosition.y += stepInGrid.y;
                t = tMax.y;
                normal = vec3(0, -stepInGrid.y, 0);
                tMax.y += tDelta.y;
            } else {
                voxelPosition.z += stepInGrid.z;
                t = tMax.z;
                normal = vec3(0, 0, -stepInGrid.z);
                tMax.z += tDelta.z;
            }
        }

    }

    hitPayload.hitDistance = -1;
    return hitPayload;
}

uint PCGHash(uint input)
{
    uint state = input * 747796405u + 2891336453u;
    uint word = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
    return (word >> 22u) ^ word;
}

float randFloat(inout uint seed){
    seed = PCGHash(seed);
    return float(seed)/float(0xFFFFFFFF);
}

vec3 randVec3(inout uint seed){
    return normalize(vec3(
        2*(randFloat(seed) - 0.5),
        2*(randFloat(seed) - 0.5),
        2*(randFloat(seed) - 0.5)
    ));
}

void main() {

    ivec2 pixelPos = ivec2(gl_GlobalInvocationID.xy);
    ivec2 screen_size = imageSize(colorbuffer);
    if (pixelPos.x >= screen_size.x || pixelPos.y >= screen_size.y) {
        return;
    }

    uint seed = uint((pixelPos.x<<16) + (pixelPos.y) + rendererRandom);

    Ray ray;

    vec2 coord = vec2( float(pixelPos.x) / float(screen_size.x), float(pixelPos.y) / float(screen_size.y));
    coord = coord * 2 - 1;

    vec4 target = inverseProjectionMatrix * vec4(coord.x, coord.y, 1, 1);
    vec3 cameraRayDirection = normalize(vec3(inverseViewMatrix * vec4(normalize(vec3(target) / target.w), 0)));


    ray.origin = cameraPosition;
    ray.direction = cameraRayDirection;
    HitPayload hitPayload;
    Material material;

    vec3 color = backgroundColor;

    seed += 1;

    hitPayload = traceRay(ray);

    if (hitPayload.hit) {
        color = vec3(0);

        material = materials[hitPayload.materialIndex];
        vec3 hitPosition = hitPayload.position;
        vec3 normal = hitPayload.normal;
        float distance_to_player = length(cameraPosition-hitPosition);

        ray.origin = hitPayload.position + hitPayload.normal * 0.001;

        //Sun light:
        ray.direction = -sun_dir;
        hitPayload = traceRay(ray);
        color += float(!hitPayload.hit) * material.albedo * sun_light_color * sun_light_intensity * max(0, dot(normal, ray.direction));

        //Player light:
        ray.direction = normalize(cameraPosition - hitPosition);
        hitPayload = traceRay(ray);

        color += float(!hitPayload.hit) * material.albedo * player_light_color * player_light_intensity * max(0, dot(normal, ray.direction))/(4*M_PI * distance_to_player * distance_to_player);
        color += float(hitPayload.hit && (hitPayload.hitDistance > distance_to_player)) * material.albedo * player_light_color * player_light_intensity * max(0, dot(normal, ray.direction))/(4*M_PI * distance_to_player * distance_to_player);

    }

    imageStore(colorbuffer, pixelPos, vec4(color, 1.0));
}
